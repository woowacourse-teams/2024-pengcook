plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.7'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'jacoco'
	id 'com.epages.restdocs-api-spec' version '0.18.2'
}

group = 'net.pengcook'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.5'
	implementation 'com.google.firebase:firebase-admin:9.3.0'
	implementation 'com.auth0:java-jwt:4.4.0'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

	runtimeOnly 'mysql:mysql-connector-java:8.0.33'
	runtimeOnly 'com.h2database:h2'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-restassured'
	testImplementation 'com.epages:restdocs-api-spec-restassured:0.18.2'
	testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.18.2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

ext {
	profile = project.hasProperty('profile') ? project.profile : 'local'
	serverUrl = 'http://localhost:8080'

	switch (profile) {
		case 'dev':
			serverUrl = 'https://dev.pengcook.net'
			break
		case 'prod':
			serverUrl = 'https://www.pengcook.net'
			break
	}
}

test {
	useJUnitPlatform()
	systemProperty 'spring.profiles.active', 'test'
}

openapi3 {
	server = serverUrl
	title = 'Pengcook API'
	description = 'Pengcook API description'
	version = '0.1.0'
	format = 'yaml'
}

tasks.register("copyOasToSwagger", Copy) {
	dependsOn("openapi3")

	from layout.buildDirectory.file("api-spec/openapi3.yaml").get()
	into "src/main/resources/static"
}

tasks.named("processResources") {
	dependsOn copyOasToSwagger
}

tasks.named("bootJar") {
	dependsOn copyOasToSwagger
}

tasks.named("jar") {
	enabled = false
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		csv.required = false
		html.required = false
	}
}
